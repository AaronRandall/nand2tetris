// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU. Computes one of the following functions:
 * x+y, x-y, y–x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs.
 * Which function to compute is determined by 6 input bits 
 * denoted zx, nx, zy, ny, f, no.
 * The computed function's value is called "out".
 * In addition to computing out, the ALU computes two 
 * 1-bit outputs called zr and ng:
 * if out == 0, zr = 1; otherwise zr = 0;
 * If out < 0, ng = 1; otherwise ng = 0.
 * The 6-bit combinations (zx,nx,zy,ny,f,no) and 
 * their effect are documented in the book. 
 */

// Implementation: the ALU manipulates the x and y
// inputs and then operates on the resulting values, 
// as follows:
// if (zx  == 1) sets x = 0        // 16-bit constant
// if (nx  == 1) sets x = ~x       // bitwise "not"
// if (zy  == 1) sets y = 0        // 16-bit constant
// if (ny  == 1) sets y = ~y       // bitwise "not"
// if (f   == 1) sets out = x + y  // integer 2's-complement addition
// if (f   == 0) sets out = x & y  // bitwise And
// if (no  == 1) sets out = ~out   // bitwise Not
// if (out == 0) sets zr = 1
// if (out < 0)  sets ng = 1

  
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute  out = x + y (if f == 1) or out = x & y (if == 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    Mux16(a[0..15]=x, b[0..15]=false, sel=zx, out[0..15]=zxoutputx);
    Not16(in[0..15]=zxoutputx, out[0..15]=notx);
    Mux16(a[0..15]=zxoutputx, b[0..15]=notx, sel=nx, out[0..15]=nxoutputx);

    Mux16(a[0..15]=y, b[0..15]=false, sel=zy, out[0..15]=zyoutputy);
    Not16(in[0..15]=zyoutputy, out[0..15]=noty);
    Mux16(a[0..15]=zyoutputy, b[0..15]=noty, sel=ny, out[0..15]=nyoutputy);

    Add16(a[0..15]=nxoutputx, b[0..15]=nyoutputy, out[0..15]=xplusy);
    And16(a[0..15]=nxoutputx, b[0..15]=nyoutputy, out[0..15]=xandy);
    Mux16(a[0..15]=xandy, b[0..15]=xplusy, sel=f, out[0..15]=fout);

    Not16(in[0..15]=fout, out[0..15]=notfout);

    // Output 'out'
    Mux16(a[0..15]=fout, b[0..15]=notfout, sel=no, out[0..7]=lowfinalout, out[8..15]=highfinalout, out[15]=ng, out=out);
    
    // Now set the zr and ng outputs
    Or8Way(in[0..7]=lowfinalout, out=lowout);
    Or8Way(in[0..7]=highfinalout, out=highout);

    // if lowout=0 and highout=0, zr = 1, else zr = 0

    Or(a=lowout, b=highout, out=aborout);
    Not(in=aborout, out=zr);


}
